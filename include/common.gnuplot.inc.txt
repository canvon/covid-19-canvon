
# Avoid duplicating what this file set up.
CommonLoaded = 1

# Allow terminal type to be given as first argument to "call".
# Fall back to qt (interactive graphics) if none was given.
TermType = exists("ARG1") && strlen(ARG1) > 0 ? ARG1 : "qt"
set terminal @TermType size 1024,768

# When using another terminal type, an output might need to be given.
TermOut = exists("ARG2") && strlen(ARG2) > 0 ? ARG2 : ""
if (TermOut eq "" && TermType eq "png") {
  TermOut = "out.png"
}
if (TermOut ne "") {
  set output TermOut
}

# General preparation for our data environment.
set datafile separator comma
set timefmt      "%Y-%m-%d"  # Reading, e.g., from plot range strings.
set xtics format "%Y-%m-%d"  # Writing to output.
set xdata time

# Switch from key at the upper-right to upper-left,
# as the plots will usually end right there. ...
set key left

# Predefine some helper functions, to make the actual plot commands shorter.
TC(Dummy) = timecolumn(1,"%s")
SelectLocation(Location, Value) = stringcolumn("location") eq Location ? Value : NaN

# Selection of locations to plot on world comparison charts.
WorldLocations = "World China Japan Germany Italy France"

# A chance to do anything else that might be needed.
@ARG3

